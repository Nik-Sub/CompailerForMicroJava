package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

// import java.io.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

init with {:
	errorDetected = false;
:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, LBRACE, RBRACE, NAMESPACE, CONST, EQUAL, COMMA, SEMI, STATIC;
terminal Integer NUMBER ;
terminal Integer TRUE ;
terminal Integer FALSE ;
terminal String IDENT ;
terminal String BOOLCONST ;
terminal String LETTER ;
terminal LBOXBRAC, RBOXBRAC, LPAREN, RPAREN, COL, IF, BREAK, ELSE, CONTINUE;
terminal RETURN, FOR, ASTERISK, INC, DEC, OR, AND, NEW, DOT, EQUALEQUAL, DIFF;
terminal GT, GET, LT, LET, PLUS, MINUS, VOID, PERCENT, DIVISION, PRINT, READ, FOLLOWS, SPACE, IDENTCOLCOL;

nonterminal NamespaceList, DeclList, MethodDeclList;
nonterminal Namespace, DeclType, ConstDecl, VarDecl;
nonterminal ConstTypeList ;
nonterminal BracketForArray, StaticInitializer, StatementList;
nonterminal Statement, TypeList, FormParsOpt, VarDeclOpt, FormPars, ListFormPars;
nonterminal IdentOpt, DesignatorStatement, ElseOpt, ExprOpt;
nonterminal NumConstOpt, DesignatorStatementOpt, CondFactOpt, CondFact;
nonterminal ListExpressions, ListDesignator, Assignop, ActParsOpt, ListExpr, DesignatorOpt;
nonterminal Condition, CondTerm, CondTermList, OptRelop, CondFactList;
nonterminal AdditionList, MinusOpt, MultiplicationList;
nonterminal OptParen, ListParams, ListIdent, Label, Relop, Addop, Mulop, ExprFactor, ActPars;
nonterminal MtchStatement, UnmtchStatement, MethodDeclVarList, SufixForIdent;
nonterminal ReturnNoExprClass, ConstInit, EndOfNamespace, NamespaceName, Boolconst;


nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, MethodDecl, MethodTypeName, Designator, KindOfVar, KindOfPar;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Term, Factor, Expr, ListVarDecl, ConstType;



precedence left ELSE;


Program ::= (ProgramClass) PROG ProgName:p NamespaceList DeclList LBRACE MethodDeclList RBRACE
			;

ProgName ::= (ProgNameClass) IDENT:progName;

NamespaceList ::=(NamespaceListClass) NamespaceList Namespace
				|
				(NoNamespaceClass) /* epsilon */
                ;

DeclList ::= (DeclListClass) DeclList DeclType
			|
			(NoDeclClass) /* epsilon */
			;

DeclType ::= (ConstDeclTypeClass) ConstDecl
			|
			(VarDeclTypeClass) VarDecl
			;

MethodDeclList ::=(MethodDeclListClass) MethodDeclList MethodDecl
				 | (NoMethodDeclListClass) /*epsilon*/
                 ;

Namespace ::= (NamespaceClass) NAMESPACE NamespaceName LBRACE DeclList LBRACE MethodDeclList RBRACE EndOfNamespace
			;
			
NamespaceName ::= (NamespaceNameClass) IDENT:Name
				;			

EndOfNamespace ::= (EndOfNamespace) RBRACE
				;

ConstDecl ::= (ConstDeclClass) CONST Type ConstInit ConstTypeList SEMI
			;
			
ConstInit ::= (ConstInitClass) IDENT:Name EQUAL ConstType:value
			;

ConstTypeList ::=(ConstTypeListClass) ConstTypeList COMMA ConstInit
				| (NoConstTypeListClass) /* epsilon */
                ;

ConstType ::= (BoolConstTypeClass) Boolconst
           | (CharConstTypeClass) LETTER:value
           | (NumConstTypeClass) NUMBER:value
           ;
           
Boolconst ::= TRUE
			|
			FALSE
			;
           
Type ::= (TypeClass) IDENT:typeName
		|
		(TypeWithPrefixClass) IDENT COL COL IDENT:typeName /* ovo je ja mislim samo ako imam klasu unutar namespacea */
		;

IdentOpt ::= (IdentOptClass) IDENT COL COL
			 |
			 (NoIdentOptClass) /* epsilon */
             ;

TypeList ::= (VoidTypeClass) VOID
          | (TypeMethodClass) Type
          ;
          

VarDecl ::= (VarDeclClass) Type:varType KindOfVar ListVarDecl SEMI
          	;
          	
          	
KindOfVar ::= (VarNoArrayClass) IDENT:name
			|
			(VarArrayClass) IDENT:name LBOXBRAC RBOXBRAC
			;

VarDeclOpt ::= (VarDeclOptClass) VarDecl
			|
			(NoVarDeclOptClass) /* epsilon */
            ;
            
ListVarDecl ::= (ListVarDeclClass) COMMA KindOfVar ListVarDecl 
				|
				(NoListVarDeclClass) /* epsilon */
                ;

BracketForArray ::= (BracketsForArrayClass) LBOXBRAC RBOXBRAC
				|
				(NoBracketForArrayClass) /* epsilon */
                ;

MethodDecl ::= (MethodDeclClass) MethodTypeName LPAREN FormParsOpt RPAREN MethodDeclVarList LBRACE StatementList RBRACE
			;
			
MethodTypeName ::= (ReturnTypeClass) Type:t IDENT:methName
				|
				(VoidRetTypeClass) VOID IDENT:methName
				;

FormParsOpt ::= (FormParsOptClass) FormPars
				| 
				(NoFormParsOptClass) /* epsilon */
                ;

FormPars ::= (FormParsClass) Type KindOfPar ListFormPars
            	;

ListFormPars ::= (ListFormParsClass) ListFormPars COMMA Type KindOfPar
				|
				(NoListFormParsClass) /* epsilon */
                ;

KindOfPar ::= (ParNoArrayClass) IDENT:name
			|
			(ParArrayClass) IDENT:name LBOXBRAC RBOXBRAC
			;
                
MethodDeclVarList ::= (MethodDeclVarListClass) MethodDeclVarList VarDecl 
					|
					(NoMethodDeclVarListClass) /* epsilon */
					;

StatementList ::= (StatementListClass) StatementList Statement
				| (NoStatementListClass) /* epsilon */
                ;

StaticInitializer ::= (StaticInitializerClass) STATIC LBRACE StatementList RBRACE
			;








Statement ::= (StatementClass) DesignatorStatement SEMI
           |
           (IfClass) IF LPAREN Condition RPAREN Statement ElseOpt
           |
           (BreakClass) BREAK SEMI
           |
           (ContinueClass) CONTINUE SEMI
           |
           (ReturnClass) RETURN Expr:t SEMI
           |
           (ReturnNoExpressionClass) RETURN SEMI
           |
           (ReadClass) READ LPAREN Designator RPAREN SEMI
           |
           (PrintClass) PRINT LPAREN Expr NumConstOpt RPAREN SEMI
           |
           (ForClass) FOR LPAREN DesignatorStatementOpt SEMI CondFactOpt SEMI DesignatorStatementOpt RPAREN Statement
           |
           (StatementListClassBranch) LBRACE StatementList RBRACE
           ;




DesignatorStatement	::= (AssignOperatorClass) Designator:dest Assignop Expr:e
					|
					(ParenOperatorClass) Designator:dest LPAREN ActParsOpt RPAREN
					|
					(DecClass) Designator:dest INC
					|
					(IncClass) Designator:dest DEC 
					|
					(DesignatorStatementWithArrayClass) LBOXBRAC ListDesignator ASTERISK Designator RBOXBRAC EQUAL Designator
					;

Designator			::= (DesignatorClass) IDENT:desigName ListIdent
					|
					(DesignatorWithPrefixClass) IDENT:prefix COL COL IDENT:desigName ListIdent
					;
					
					
ListIdent			::= (ListIdentClass) ListIdent SufixForIdent
					|
					(NoListIdentClass) /* epsilon */
					;
					
SufixForIdent 		::= (IdentListIdentClass) DOT IDENT
					|
					(ExprListIdentClass) LBOXBRAC Expr RBOXBRAC
					;

ActPars 			::= (ActParsClass) Expr ListExpr
					;

ActParsOpt			::= (ActParsOptClass) ActPars
					|
					(NoActParsOptClass) /* epsilon */
					;	
					
Expr				::= (AddExprClass) Expr:te Addop Term:t
					|
					(ExprClass) MinusOpt Term:te
					;					
MinusOpt			::= (MinusOptClass) MINUS
					|
					(NoMinusOptClass) /* epsilon */
					;

AdditionList		::= (AdditionListClass) Addop Term AdditionList
					|
					(NoAdditionListClass) /* epsilon */
					;
					
					
Term 				::= (TermClass) Factor MultiplicationList
					;

MultiplicationList	::= (MultiplicationListClass) Mulop Factor MultiplicationList
					|
					(NoMultiplicationListClass) /* epsilon */
					;

Factor				::= (ExprFactorClass) LPAREN Expr RPAREN
					|
					(NewTypeClass) NEW Type:nameOfType ListParams
					|
					(BoolConstClass) BOOLCONST
					|
					(LetterClass) LETTER
					|
					(NumberClass) NUMBER
					|
					(FunCallClass) Designator:func LPAREN ActParsOpt RPAREN
					|
					(VarClass) Designator:d
					;

OptParen 			::= (OptParenClass) LPAREN ActParsOpt RPAREN
					|
					(NoOptParenClass) /* epsilon */
					;

ListParams			::= (ListParamsActParsOptClass) LPAREN ActParsOpt RPAREN
					|
					(ListParamsExprExprClass) LBOXBRAC Expr RBOXBRAC
					;
				
					
NumConstOpt			::=
					(NumConstOptClass) COMMA NUMBER
					|
					(NoNumConstClass) /* epsilon */
					;			
					



ElseOpt ::= (ElseOptClass) ELSE Statement
			|
			(NoElseOptClass) /* epsilon */
			;

ExprOpt				::= (ExprOptClass) Expr
					|
					(NoExprClass) /* epsilon */
					;



DesignatorStatementOpt ::= (DesignatorStatementTerminalClass) DesignatorStatement
						|
						(DesignatorStatementListClass) DesignatorStatementOpt COMMA DesignatorStatement
						|
						(NoDesignatorStatementOptClass) /* epsilon */
						;






CondFact			::= (CondFactClass) Expr OptRelop
					;


CondFactOpt			::= (CondFactOptClass) CondFact
					|
					(NoCondFactOptClass) /* epsilon */
					;




ListExpr			::= (ListExprClass) ListExpr COMMA Expr
					|
					(NoListExprClass) /* epsilon */
					;

ListDesignator		::= (ListDesignatorClass) ListDesignator DesignatorOpt COMMA
					|
					(NoListDesignatorClass) /* epsilon */
					;

DesignatorOpt		::= (DesignaorOptClass) Designator
					|
					(NoDesignatorOptClass) /* epsilon */
					;

Condition			::= (CondtionClass) CondTerm CondTermList
					;

CondTermList		::= (CondTermListClass) OR CondTerm CondTermList
					|
					(NoCondTermListClass) /* epsilon */
					;

CondTerm			::= (CondTermClass) CondFact CondFactList
					;

CondFactList		::= (CondFactListClass) AND CondFact CondFactList
					|
					(NoCondFactListClass) /* epsilon */
					;



OptRelop			::= (OptRelopClass) Relop Expr
					|
					(NoOptRelopClass) /* epsilon */
					;


















Label				::= (LabelIdentClass) IDENT
					;

Assignop			::= (AssignopClass) EQUAL
					;

Relop				::= (LetClass) LET
					|
					(LtClass) LT
					|
					(GetClass) GET
					|
					(GtClass) GT
					|
					(DiffClass) DIFF
					|
					(EqualequalClass) EQUALEQUAL
					;

Addop				::= (MinusOpClass) MINUS
					|
					(AddOpClass) PLUS
					;

Mulop				::= (PercentClass) PERCENT
					|
					(DivisionClass) DIVISION
					|
					(AsteriskClass) ASTERISK
					;
 
               